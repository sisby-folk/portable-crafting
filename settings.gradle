pluginManagement {
	repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		// Currently needed for Intermediary and other temporary dependencies
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		gradlePluginPortal()
	}
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def mc_major = '1.19'
			def mc = mc_major + '.4'
			def fapi = "0.83.0"

			// // Build Tools // //
			version('loom', '1.2.+')

			plugin('loom', 'org.quiltmc.loom').versionRef('loom')

			// // Base // //
			version('mc', mc)
			version('ql', "0.19.0")
			version('qm', "${mc}+build.10")
			version('qsl', "5.0.0-beta.10+${mc}")
			version('fapi', "${fapi}-${mc}")
			version('qfapi', "6.0.0-beta.9+${fapi}-${mc}")

			library('mc', 'mojang', 'minecraft').versionRef('mc')
			library('ql', 'org.quiltmc', 'quilt-loader').versionRef('ql')
			library('qm', 'org.quiltmc', 'quilt-mappings').versionRef('qm')
			library('qsl', 'org.quiltmc', 'qsl').versionRef('qsl')
			library('qfapi', 'org.quiltmc.quilted-fabric-api', 'quilted-fabric-api').versionRef('qfapi')

			// // Libraries // //


			// // Compat // //
			version('inventoryTabs', "0.9.1-1.19.4-beta")
			version('clothConfig', "10.0.96")

			library('inventoryTabs', 'maven.modrinth', 'inventory-tabs-updated').versionRef('inventoryTabs')
			library('clothConfig', 'me.shedaniel.cloth', 'cloth-config-fabric').versionRef('clothConfig')

		}
	}
}
